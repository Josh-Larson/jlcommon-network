plugins {
	id 'java'
	id 'application'
	id "com.jfrog.bintray" version "1.8.4"
	id 'maven'
	id 'maven-publish'
	id 'idea'
	id "org.javamodularity.moduleplugin" version "1.3.0"
}

def projectGroup = 'me.joshlarson'
def projectName = 'jlcommon-network'
def projectVersion = '1.0.0'
def projectModule = 'me.joshlarson.jlcommon.network'

group = projectGroup
version = projectVersion
sourceCompatibility = 11
targetCompatibility = 11

repositories {
	jcenter()
}

dependencies {
	compile group: 'me.joshlarson', name: 'jlcommon', version: '1.9.0'
	compile group: 'org.jetbrains', name: 'annotations', version: '16.0.3'
	
	testCompile 'junit:junit:4.12'
}

jar {
	archiveBaseName.set(projectName)
	archiveClassifier.set(null)
	archiveVersion.set(projectVersion)
	from sourceSets.main.allSource
}

javadoc {
	inputs.property("moduleName", projectModule)
	doFirst {
		options.addStringOption('-module-path', classpath.asPath)
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveBaseName.set(projectName)
	archiveClassifier.set("sources")
	archiveVersion.set(projectVersion)
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveBaseName.set(projectName)
	archiveClassifier.set("javadoc")
	archiveVersion.set(projectVersion)
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

// Create the publication with the pom configuration:
publishing {
	publications {
		jlcommonnetwork(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId = projectGroup
			artifactId = projectName
			version projectVersion
			pom {
				name = 'jlcommon-network'
				description = 'A Common Java Networking Library'
				url = 'https://github.com/Josh-Larson/jlcommon-network'
				licenses {
					license {
						name = 'The MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'Josh-Larson'
						name = 'Josh Larson'
						email = 'joshua-github@ourclan.net'
					}
				}
				scm {
					url = 'https://github.com/Josh-Larson/jlcommon-network'
				}
			}
		}
	}
}

bintray {
	user ((String) (project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')))
	key ((String) (project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')))
	publications 'jlcommonnetwork'
	pkg {
		repo = 'jlcommon'
		name = projectName
		licenses = ['MIT']
		vcsUrl = 'https://github.com/Josh-Larson/jlcommon-network'
		version {
			name = projectVersion
			desc = 'Common Java Networking Library'
			released = new Date()
			vcsTag = 'v' + projectVersion
		}
	}
}
